plugins {
  id 'java-library'
  id 'com.google.protobuf' version '0.9.4'
}

evaluationDependsOn(":vertx-grpc-protoc-plugin2")

sourceSets {
  main {
    java {
      exclude 'examples/**'
    }
  }
  examples {
    java {
      srcDir 'src/main/java/examples'
    }
    resources {
      srcDir 'src/main/proto'
    }
  }
}

configurations {
  examplesImplementation
  examplesRuntimeOnly
  protocPlugin
}

dependencies {
  api "io.vertx:vertx-core:$vertxVersion"
  api project(':vertx-grpc-common')

  implementation "io.vertx:vertx-auth-common:$vertxVersion"
  implementation "io.vertx:vertx-web:$vertxVersion"
  implementation("io.grpc:grpc-protobuf:$grpcIoVersion") {
    exclude group: 'com.google.guava', module: 'guava'
  }

  implementation("io.grpc:grpc-stub:$grpcIoVersion") {
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
  }
  implementation("io.grpc:grpc-api:$grpcIoVersion") {
    exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
    exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
  }

  testCompileOnly "javax.annotation:javax.annotation-api:1.3.2"

  testImplementation project(':vertx-grpc-client')
  testImplementation project(path: ':vertx-grpc-common', configuration: 'testClasses')
  testImplementation "io.vertx:vertx-unit:$vertxVersion"
  testImplementation "org.bouncycastle:bcpkix-jdk15on:1.65"
  testImplementation("io.grpc:grpc-netty:$grpcIoVersion") {
    exclude group: '*', module: '*'
  }

  examplesImplementation project(':vertx-grpc-common')
  examplesImplementation("io.grpc:grpc-protobuf:$grpcIoVersion") {
    exclude group: 'com.google.guava', module: 'guava'
  }
  examplesImplementation "io.grpc:grpc-stub:$grpcIoVersion"
  examplesImplementation "io.grpc:grpc-api:$grpcIoVersion"

  protocPlugin project(path: ':vertx-grpc-protoc-plugin2', configuration: 'shadow')

}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:3.25.5"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:$grpcIoVersion"
    }
    vertx {
      path = tasks.getByPath(':vertx-grpc-protoc-plugin2:shadowJar').archiveFile.get().asFile
    }
  }
  generateProtoTasks {
    all().each { task ->
      task.plugins {
        grpc {}
        vertx {}
      }
      task.dependsOn ':vertx-grpc-protoc-plugin2:shadowJar'
    }
    ofSourceSet('examples').each { task ->
      task.plugins {
        grpc {}  // For regular gRPC classes
        vertx {} // For Vert.x specific classes
      }
    }
  }
}
